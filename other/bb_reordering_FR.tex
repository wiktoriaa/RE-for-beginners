\section{Réordonnancement des blocs élémentaires}

% TODO __builtin_expect in GCC?

\subsection{Optimisation guidée par profil}
\label{PGO}

\myindex{\oracle}
\myindex{Intel C++}

Cette méthode d'optimisation déplace certains \gls{basic block}s vers d'autres sections du fichier 
binaire exécutable.

Il est évident que certaines parties d'une fonction sont exécutées plus fréquemment que d'autres
(ex: le corps d'une boucle) et d'autres moins souvent (ex: gestionnaire d'erreur, gestionnaires 
d'exception).

Le compilateur ajoute dans le code exécutable des instructions d'instrumentation. Le développeur 
exécute ensuite un nombre important de tests, ce qui permet de collecter des statistiques.

A l'aider de ces dernières, le compilateur prépare le fichier exécutable final en déplacant les 
fragments de code les moins exécutés vers une autre section.

Tous les fragments de code les plus souvent exécutés sont ainsi regroupées, ce qui constitue un 
facteur important pour la rapidité d'exécution et l'utilisation du cache.

Voici un exemple de code \oracle produit par le compilateur Intel C++:

\begin{lstlisting}[caption=orageneric11.dll (win32),style=customasmx86]
                public _skgfsync
_skgfsync       proc near

; address 0x6030D86A

                db      66h
                nop
                push    ebp
                mov     ebp, esp
                mov     edx, [ebp+0Ch]
                test    edx, edx
                jz      short loc_6030D884
                mov     eax, [edx+30h]
                test    eax, 400h
                jnz     __VInfreq__skgfsync  ; write to log
continue:
                mov     eax, [ebp+8]
                mov     edx, [ebp+10h]
                mov     dword ptr [eax], 0
                lea     eax, [edx+0Fh]
                and     eax, 0FFFFFFFCh
                mov     ecx, [eax]
                cmp     ecx, 45726963h
                jnz     error                ; exit with error
                mov     esp, ebp
                pop     ebp
                retn
_skgfsync       endp

...

; address 0x60B953F0

__VInfreq__skgfsync:
                mov     eax, [edx]
                test    eax, eax
                jz      continue
                mov     ecx, [ebp+10h]
                push    ecx
                mov     ecx, [ebp+8]
                push    edx
                push    ecx
                push    offset ... ; "skgfsync(se=0x%x, ctx=0x%x, iov=0x%x)\n"
                push    dword ptr [edx+4]
                call    dword ptr [eax] ; write to log
                add     esp, 14h
                jmp     continue

error:
                mov     edx, [ebp+8]
                mov     dword ptr [edx], 69AAh ; 27050 "function called with invalid FIB/IOV structure"
                mov     eax, [eax]
                mov     [edx+4], eax
                mov     dword ptr [edx+8], 0FA4h ; 4004
                mov     esp, ebp
                pop     ebp
                retn
; END OF FUNCTION CHUNK FOR _skgfsync
\end{lstlisting}

La distance entre ces deux fragments de code avoisine les 9 Mo.

Tous les fragments de code rarement exécutés sont regroupés à la fin de la section de code de la DLL.

La partie de la fonction qui a été déplacée était marquée par le compilateur Intel C++ avec le 
préfixe \TT{VInfreq}.

Nous voyons donc qu'une partie de la fonction qui écrit dans un fichier journal (probablement à la 
suite d'une erreur ou d'un avertissement) n'a sans doute pas été exécuté très souvent durant les 
tests effectués par les développeurs Oracle lors de la collecte des statistiques. Il n'est même pas 
dit qu'elle ait jamais été exécutée.

Le bloc élémentaire qui écrit dans le journal s'achève par un retour à la partie \q{hot} de la 
fonction.

Un autre bloc élémentaire \q{infrequent} est celui qui retourne le code erreur 27050.

Pour ce qui est des fichiers Linux au format ELF, le compilateur Intel C++ déplace tous les 
fragments de code rarement exécutés vers une section séparée nommée \TT{text.unlikely}. 
Les fragments les plus souvent exécutés sont quant à eux regroupés dans la section \TT{text.hot}.

Cette information peut aider le rétro ingénieur à distinguer la partie principale d'une fonction 
des parties qui assurent la gestion d'erreurs.
